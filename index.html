<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <title>WebGL Electric Fields</title>
    <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/4.0.0/css/bootstrap.min.css">
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/popper.js/1.12.9/umd/popper.min.js"></script>
    <script src="https://maxcdn.bootstrapcdn.com/bootstrap/4.0.0/js/bootstrap.min.js"></script>
    <script src="https://use.fontawesome.com/releases/v6.4.2/js/all.js"></script>
    <link href="style.css" rel="stylesheet">
</head>
<body>
    <script id="Vertex-shader" type="vertex">
        #version 300 es

        layout (location = 0) in vec3 position;
        layout (location = 1) in vec3 normal;

        out vec3 vPos;
        out vec3 vNormal;

        uniform mat4 uViewProjection;
        uniform mat4 uModel;
        uniform float radius;

        void main()
        {
            vec4 pos = vec4(position * radius, 1.0);
            vPos = (uModel * pos).xyz;
            vNormal = (uModel * vec4(normal, 0.0)).xyz;
            gl_Position = uViewProjection * uModel * pos;
        }
    </script>
    <script id="Fragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        in vec3 vPos;
        in vec3 vNormal;
        out vec4 fragColor;

        uniform float uCharge;

        vec3 high_charge = vec3(0.333, 1.0, 0.1294);
        vec3 low_charge = vec3(1.0, 0.541, 0.290);

        void main()
        {
            vec3 light_dir = normalize(vec3(5, 5, 5) - vPos);
            vec3 normal = normalize(vNormal);
            float light = clamp(dot(light_dir, normal), 0.0, 1.0) * 0.5;

            light += 0.5;

            fragColor = vec4((mix(low_charge, high_charge, (uCharge / 20.0) + 0.5) * light), 1.0);
        }
    </script>
    <script id="GizmosVertex-shader" type="vertex">
        #version 300 es

        layout (location = 0) in vec3 position;

        uniform mat4 uViewProjection;
        uniform mat4 uModel;

        void main()
        {
            vec4 pos = vec4(position, 1.0);
            gl_Position = uViewProjection * uModel * pos;
        }
    </script>
    <script id="GizmosFragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        out vec4 fragColor;

        uniform vec4 uColor;

        void main()
        {
            fragColor = uColor;
        }
    </script>
    <script id="PickingFragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        in vec3 vPos;
        in vec3 vNormal;
        out vec4 fragColor;

        uniform vec4 uColor;

        void main()
        {
            fragColor = uColor;
        }
    </script>
    <script id="GridVertex-shader" type="vertex">
        #version 300 es

        layout (location = 0) in vec3 position;

        uniform mat4 uViewProjection;
        uniform mat4 uModel;

        void main()
        {
            vec4 pos = vec4(position, 1.0);
            gl_Position = uViewProjection * uModel * pos;
        }
    </script>
    <script id="GridFragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        out vec4 fragColor;

        void main()
        {
            fragColor = vec4(0.2, 0.2, 0.2, 1.0);
        }
    </script>
    <script id="ArrowVertex-shader" type="vertex">
        #version 300 es

        layout (location = 0) in vec3 position;
        layout (location = 1) in vec3 color;
        layout (location = 2) in mat4 model;

        out vec3 Color;

        uniform mat4 uViewProjection;

        void main()
        {
            vec4 pos = vec4(position, 1.0);
            gl_Position = uViewProjection * model * pos;
            Color = color;
        }
    </script>
    <script id="ArrowFragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        in vec3 Color;

        out vec4 fragColor;

        void main()
        {
            fragColor = vec4(Color, 1.0);
        }
    </script>
    <script id="ArrowComputeVertex-shader" type="vertex">
        # version 300 es

        layout (location = 0) in vec3 vpos; // position of the vector

        out vec3 color;     // output color
        out mat4 transform; // output transform

        uniform mat4 grid_matrix;

        uniform float[25] lg_nodes;
        uniform float[25] lg_weights;
        uniform float[51] gk_nodes;
        uniform float[51] gk_weights;

        float E0 = 1.0;
        float pi = 3.14159265358979;

        float min_color = 0.01;
        float max_color = 1.0;

        struct Point {
            vec3 pos;
            float q;
        };
        struct LineSegment {
            vec3 a;
            vec3 b;
            float q;
        };
        struct Plane {
            vec3 normal;
            vec3 pos;
            float sigma;
        };
        struct Ring {
            vec3 normal;
            vec3 pos;
            float radius;
            float q;
            mat4 model;
            mat4 inverse;
        };
        struct Disc {
            vec3 normal;
            vec3 pos;
            float radius;
            float q;
            mat4 model;
            mat4 inverse;
        };
        struct Washer {
            vec3 normal;
            vec3 pos;
            float inner;
            float outer;
            float q;
            mat4 model;
            mat4 inverse;
        };

        [shapes]

        float map(float value, float min1, float max1, float min2, float max2) {
            return min2 + (value - min1) * (max2 - min2) / (max1 - min1);
        }

        vec3 computePoint(Point point, vec3 pos)
        {
            vec3 dist_v = pos - point.pos;
            float r2 = dot(dist_v, dist_v);
            float E = 1.0 / (4.0 * pi * E0) * (point.q / r2);
            vec3 Exyz = normalize(dist_v);

            Exyz *= E;

            return Exyz;
        }

        vec3 computeLineSegment(LineSegment ls, vec3 pos)
        {
            float subdivisions = 100.0;
            vec3 Exyz = vec3(0.0, 0.0, 0.0);
            float dq = ls.q / subdivisions;
            for(float i = 0.0; i < subdivisions; ++i)
            {
                vec3 p = mix(ls.a, ls.b, (i / subdivisions) + (1.0 / (subdivisions * 2.0))); // middle of dl

                vec3 dist_v = pos - p;
                float r2 = dot(dist_v, dist_v);
                float dE = 1.0 / (4.0 * pi * E0) * (dq / r2);
                vec3 dExyz = normalize(dist_v);
                dExyz *= dE;
                Exyz += dExyz;
            }
            return Exyz;
        }

        vec3 computePlane(Plane plane, vec3 pos)
        {
            vec3 dist_v = pos - plane.pos;
            vec3 Exyz = plane.sigma * (plane.normal * sign(dot(dist_v, plane.normal))) / (2.0 * E0);
            return Exyz;
        }

        vec3 computeGKRing(Ring ring, vec3 pos)
        {
             vec3 P = (ring.inverse * vec4(pos, 1.0)).xyz; // from world to disc coordinates

            vec3 Exyz = vec3(0.0, 0.0, 0.0);

            float K = ring.q / (8.0 * pi * pi * ring.radius * E0);

            for(int i = 0; i < gk_nodes.length(); ++i) // r
            {
                float nTheta = (gk_nodes[i] + 1.0) / 2.0 * 2.0 * pi;
                float wTheta = gk_weights[i] * 2.0 * pi / 2.0;

                // finding distance squared of position
                float prx = P.x - ring.radius * cos(nTheta);
                float prx2 = prx * prx;

                float pry = P.y - ring.radius * sin(nTheta);
                float pry2 = pry * pry;

                float pz2 = P.z * P.z;

                float lenA2 = prx2 + pry2 + pz2;
                float lenA32 = lenA2 * sqrt(lenA2); // lenA2^(3/2)

                // calculating dExyz
                Exyz.x += wTheta * prx / lenA32;
                Exyz.y += wTheta * pry / lenA32;
                Exyz.z += wTheta * P.z / lenA32;
            }
            Exyz *= K;
            Exyz = (ring.model * vec4(Exyz, 0.0)).xyz; // from disc to world vector
            return Exyz;
        }

        vec3 computeGKDisc(Disc disc, vec3 pos)
        {
            vec3 P = (disc.inverse * vec4(pos, 1.0)).xyz; // from world to disc coordinates

            vec3 Exyz = vec3(0.0, 0.0, 0.0);

            float sigma = disc.q / (pi * disc.radius * disc.radius);

            float K = 1.0 / (4.0 * pi * E0);
            K *= sigma;

            for(int i = 0; i < gk_nodes.length(); ++i) // r
            {
                for(int j = 0; j < gk_nodes.length(); ++j) // theta
                {
                    // calculating scaled nodes and weights (equations taken from https://en.wikipedia.org/wiki/Gauss%E2%80%93Kronrod_quadrature_formula)
                    float nR = (gk_nodes[i] + 1.0) / 2.0 * disc.radius;  // node for r
                    float nTheta = (gk_nodes[j] + 1.0) / 2.0 * 2.0 * pi; // node for theta

                    float wR = gk_weights[i] * disc.radius / 2.0;        // weight for r
                    float wTheta = gk_weights[j] * 2.0 * pi / 2.0;       // weight for theta

                    // finding distance squared of position
                    float prx = P.x - nR * cos(nTheta);
                    float prx2 = prx * prx;

                    float pry = P.y - nR * sin(nTheta);
                    float pry2 = pry * pry;

                    float pz2 = P.z * P.z;

                    float lenA2 = prx2 + pry2 + pz2;
                    float lenA32 = lenA2 * sqrt(lenA2); // lenA2^(3/2)

                    // calculating dExyz
                    Exyz.x += wR * wTheta * prx / lenA32 * nR;
                    Exyz.y += wR * wTheta * pry / lenA32 * nR;
                    Exyz.z += wR * wTheta * P.z / lenA32 * nR;
                }
            }
            Exyz *= K;
            Exyz = (disc.model * vec4(Exyz, 0.0)).xyz; // from disc to world vector
            return Exyz;
        }

        vec3 computeGKWasher(Washer washer, vec3 pos)
        {
            vec3 P = (washer.inverse * vec4(pos, 1.0)).xyz; // from world to disc coordinates

            vec3 Exyz = vec3(0.0, 0.0, 0.0);

            float K = washer.q / (4.0 * pi * pi * washer.outer * washer.outer * E0);
            K -= washer.q / (4.0 * pi * pi * washer.inner * washer.inner * E0);

            for(int i = 0; i < gk_nodes.length(); ++i) // r
            {
                for(int j = 0; j < gk_nodes.length(); ++j) // theta
                {
                    // calculating scaled nodes and weights
                    float nR = (gk_nodes[i] + 1.0) / 2.0 * (washer.outer - washer.inner) + washer.inner;  // node for r
                    float nTheta = (gk_nodes[j] + 1.0) / 2.0 * 2.0 * pi;                                  // node for theta

                    float wR = gk_weights[i] * (washer.outer - washer.inner) / 2.0;        // weight for r
                    float wTheta = gk_weights[j] * 2.0 * pi / 2.0;                         // weight for theta

                    // finding distance squared of position
                    float prx = P.x - nR * cos(nTheta);
                    float prx2 = prx * prx;

                    float pry = P.y - nR * sin(nTheta);
                    float pry2 = pry * pry;

                    float pz2 = P.z * P.z;

                    float lenA2 = prx2 + pry2 + pz2;
                    float lenA32 = lenA2 * sqrt(lenA2); // lenA2^(3/2)

                    // calculating dExyz
                    Exyz.x += wR * wTheta * prx / lenA32 * nR;
                    Exyz.y += wR * wTheta * pry / lenA32 * nR;
                    Exyz.z += wR * wTheta * P.z / lenA32 * nR;
                }
            }
            Exyz *= K;
            Exyz = (washer.model * vec4(Exyz, 0.0)).xyz; // from disc to world vector
            return Exyz;
        }

        void main()
        {
            vec3 z;

            [calculate]

            float mag = length(z);

            // generate color
            float col = clamp(mag, min_color, max_color);
            col = map(col, min_color, max_color, 0.0, 1.0);
            color = mix(vec3(0.0, 0.5, 0.5), vec3(1.0, 0.0, 0.0), col); //(0.0, 0.0117, 0.6784) (0.941, 0.2039, 0.1215)

            // clamp z between min and max value
            float arrow_size = clamp(mag, 0.01, 1.0);
            z *= (arrow_size / mag);

            // calculate orientation matrix
            vec3 x = normalize(cross(vpos, vec3(0.0003, 1.0, 0.0001))) * arrow_size;
            vec3 y = normalize(cross(z, x)) * arrow_size;

            transform[0] = vec4(x, 0.0);//vec4(x, vpos.x);
            transform[1] = vec4(y, 0.0);//vec4(y, vpos.y);
            transform[2] = vec4(z, 0.0);//vec4(z, vpos.z);
            transform[3] = vec4(vpos, 1.0);
        }
    </script>
    <script id="ArrowComputeFragment-shader" type="fragment">
        #version 300 es
        precision highp float;
        void main() {}
    </script>
    <div class="container" style="background-color: darkslategray; color: white; border-radius: 1rem">
        <div class="row">
            <div class="col-sm-3 text-center" id="controls">
                <div class="dropdown d-inline">
                    <button class="btn btn-secondary dropdown-toggle menu-input" type="button" id="dropdownMenuButton" data-toggle="dropdown" aria-haspopup="true" aria-expanded="false">
                        Shapes
                    </button>
                    <div class="dropdown-menu" aria-labelledby="dropdownMenuButton">
                        <a class="dropdown-item" id="point-option">
                            <div>
                                <i class="fa-solid fa-circle fa-2xs" style="color: dimgray"></i>
                                Point
                            </div>
                        </a>
                        <a class="dropdown-item" id="line-segment-option">
                            <div>
                                <i class="fa-solid fa-minus fa-rotate-90" style="color: dimgray"></i>
                                Line Segment
                            </div>
                        </a>
                        <a class="dropdown-item" id="plane-option">
                            <div>
                                <i class="fa-solid fa-square-full" style="color: dimgray"></i>
                                Infinite Plane
                            </div>
                        </a>
                        <a class="dropdown-item" id="ring-option">
                            <div>
                                <i class="fa-regular fa-circle" style="color: dimgray"></i>
                                Ring
                            </div>
                        </a>
                        <a class="dropdown-item" id="disc-option">
                            <div>
                                <i class="fa-solid fa-circle" style="color: dimgray"></i>
                                Disc
                            </div>
                        </a>
                        <a class="dropdown-item" id="washer-option">
                            <div>
                                <i class="fa-solid fa-circle-dot" style="color: dimgray"></i>
                                Washer
                            </div>
                        </a>
                        <hr />
                        <a class="dropdown-item" id="parallel-plate-option">
                            <div>
                                <i class="fa-solid fa-grip-lines-vertical" style="color: dimgray"></i>
                                Parallel Plate
                            </div>
                        </a>
                    </div>
                </div>
                <button class="btn btn-secondary d-inline menu-input" type="button" id="clear-button">Clear</button>
                <hr />
                <!--<p><button id="add-charge-button">Add Charge</button></p>
                <input type="range" min="1" max="100" value="10" id="test-slider">-->
                <div id="charge-control-container">
                    <!--<div class="charge-panel" id="selected-charge">
                        <div class="charge-header">
                            <p class="charge-name">Washer</p>
                            <div class="delete-icon-container">
                                <i class="fa-solid fa-xmark delete-icon"></i>
                            </div>
                        </div>
                        <div class="charge-input">
                            <p class="charge-input-label">q:</p>
                            <input class="charge-input-box" type="number" />
                        </div>
                        <div class="charge-input">
                            <p class="charge-input-label">inner:</p>
                            <input class="charge-input-box" type="number" />
                        </div>
                        <div class="charge-input">
                            <p class="charge-input-label">outer:</p>
                            <input class="charge-input-box" type="number" />
                        </div>
                    </div>
                    <hr />
                    <div class="charge-panel">
                        <div class="charge-header">
                            <p class="charge-name">Ring</p>
                            <div class="delete-icon-container">
                                <i class="fa-solid fa-xmark delete-icon"></i>
                            </div>
                        </div>
                        <div class="charge-input">
                            <p class="charge-input-label">q:</p>
                            <input class="charge-input-box" type="number" />
                        </div>
                        <div class="charge-input">
                            <p class="charge-input-label">radius:</p>
                            <input class="charge-input-box" type="number" />
                        </div>
                    </div>
                    <hr />
                    <div class="charge-panel">
                        <div class="charge-header">
                            <p class="charge-name">Point</p>
                            <div class="delete-icon-container">
                                <i class="fa-solid fa-xmark delete-icon"></i>
                            </div>
                        </div>
                        <div class="charge-input">
                            <p class="charge-input-label">q:</p>
                            <input class="charge-input-box" type="number" />
                        </div>
                    </div>
                    <hr />
                        -->
                </div>
            </div>
            <div class="col-9" id="canvas-container">
                <canvas id="c"></canvas>
            </div>
        </div>
    </div>
    <script src="gl-matrix.js"></script>
    <script src="gl-utils.js"></script>
    <script src="shapes.js"></script>
    <script src="gizmos.js"></script>
    <script src="application.js"></script>
</body>
</html>