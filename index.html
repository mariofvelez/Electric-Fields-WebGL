<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8" />
    <title>WebGL Test</title>
</head>
<body>
    <script id="Vertex-shader" type="vertex">
        #version 300 es

        layout (location = 0) in vec3 position;
        layout (location = 1) in vec3 normal;

        out vec3 vPos;
        out vec3 vNormal;

        uniform mat4 uViewProjection;
        uniform mat4 uModel;
        uniform float radius;

        void main()
        {
            vec4 pos = vec4(position * radius, 1.0);
            vPos = (uModel * pos).xyz;
            vNormal = (uModel * vec4(normal, 0.0)).xyz;
            gl_Position = uViewProjection * uModel * pos;
        }
    </script>
    <script id="Fragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        in vec3 vPos;
        in vec3 vNormal;
        out vec4 fragColor;

        uniform float uCharge;

        vec3 high_charge = vec3(0.333, 1.0, 0.1294);
        vec3 low_charge = vec3(1.0, 0.541, 0.290);

        void main()
        {
            vec3 light_dir = normalize(vec3(5, 5, 5) - vPos);
            vec3 normal = normalize(vNormal);
            float light = clamp(dot(light_dir, normal), 0.0, 1.0) * 0.5;

            light += 0.5;

            fragColor = vec4((mix(low_charge, high_charge, (uCharge / 20.0) + 0.5) * light), 1.0);
        }
    </script>
    <script id="GridVertex-shader" type="vertex">
        #version 300 es

        layout (location = 0) in vec3 position;

        uniform mat4 uViewProjection;
        uniform mat4 uModel;

        void main()
        {
            vec4 pos = vec4(position, 1.0);
            gl_Position = uViewProjection * uModel * pos;
        }
    </script>
    <script id="GridFragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        out vec4 fragColor;

        void main()
        {
            fragColor = vec4(0.2, 0.2, 0.2, 1.0);
        }
    </script>
    <script id="ArrowVertex-shader" type="vertex">
        #version 300 es

        layout (location = 0) in vec3 position;
        layout (location = 1) in vec3 color;
        layout (location = 2) in mat4 model;

        out vec3 Color;

        uniform mat4 uViewProjection;

        void main()
        {
            vec4 pos = vec4(position, 1.0);
            gl_Position = uViewProjection * model * pos;
            Color = color;
        }
    </script>
    <script id="ArrowFragment-shader" type="fragment">
        #version 300 es
        precision highp float;

        in vec3 Color;

        out vec4 fragColor;

        void main()
        {
            fragColor = vec4(Color, 1.0);
        }
    </script>
    <script id="ArrowComputeVertex-shader" type="vertex">
        # version 300 es

        layout (location = 0) in vec3 vpos; // position of the vector

        out vec3 color;     // output color
        out mat4 transform; // output transform

        uniform mat4 grid_matrix;
        uniform mat4 model;
        uniform mat4 inverse;

        uniform float[25] lg_nodes;
        uniform float[25] lg_weights;
        uniform float[51] gk_nodes;
        uniform float[51] gk_weights;

        float E0 = 1.0;
        float pi = 3.14159265358979;

        float min_color = 0.01;
        float max_color = 1.0;

        // shapes go here
        uniform vec3 pos; // testing shapes
        uniform vec3 pos2;
        uniform float charge;

        struct Point {
            vec3 pos;
            float q;
        };
        struct LineSegment {
            vec3 a;
            vec3 b;
            float q;
        };
        struct Plane {
            vec3 normal;
            vec3 pos;
            float sigma;
        };
        struct Ring {
            vec3 normal;
            vec3 pos;
            float radius;
            float q;
        };
        struct Disc {
            vec3 normal;
            vec3 pos;
            float radius;
            float sigma;
        };
        struct Washer {
            vec3 normal;
            vec3 pos;
            float inner;
            float outer;
            float sigma;
        };

        float map(float value, float min1, float max1, float min2, float max2) {
            return min2 + (value - min1) * (max2 - min2) / (max1 - min1);
        }

        vec3 computePoint(Point point, vec3 pos)
        {
            vec3 dist_v = pos - point.pos;
            float r2 = dot(dist_v, dist_v);
            float E = 1.0 / (4.0 * pi * E0) * (point.q / r2);
            vec3 Exyz = normalize(dist_v);

            Exyz *= E;

            return Exyz;
        }

        vec3 computeLineSegment(LineSegment ls, vec3 pos)
        {
            float subdivisions = 100.0;
            vec3 Exyz = vec3(0.0, 0.0, 0.0);
            float dq = ls.q / subdivisions;
            for(float i = 0.0; i < subdivisions; ++i)
            {
                vec3 p = mix(ls.a, ls.b, (i / subdivisions) + (1.0 / (subdivisions * 2.0))); // middle of dl

                vec3 dist_v = pos - p;
                float r2 = dot(dist_v, dist_v);
                float dE = 1.0 / (4.0 * pi * E0) * (dq / r2);
                vec3 dExyz = normalize(dist_v);
                dExyz *= dE;
                Exyz += dExyz;
            }
            return Exyz;
        }

        vec3 computePlane(Plane plane, vec3 pos)
        {
            vec3 dist_v = pos - plane.pos;
            vec3 Exyz = plane.sigma * (plane.normal * sign(dot(dist_v, plane.normal))) / (2.0 * E0);
            return Exyz;
        }

        vec3 computeGKRing(Ring ring, vec3 pos)
        {
             vec3 P = (inverse * vec4(pos, 1.0)).xyz; // from world to disc coordinates

            vec3 Exyz = vec3(0.0, 0.0, 0.0);

            float K = ring.q / (8.0 * pi * pi * ring.radius * E0);

            for(int i = 0; i < gk_nodes.length(); ++i) // r
            {
                float nTheta = (gk_nodes[i] + 1.0) / 2.0 * 2.0 * pi;
                float wTheta = gk_weights[i] * 2.0 * pi / 2.0;

                // finding distance squared of position
                float prx = P.x - ring.radius * cos(nTheta);
                float prx2 = prx * prx;

                float pry = P.y - ring.radius * sin(nTheta);
                float pry2 = pry * pry;

                float pz2 = P.z * P.z;

                float lenA2 = prx2 + pry2 + pz2;
                float lenA32 = lenA2 * sqrt(lenA2); // lenA2^(3/2)

                // calculating dExyz
                Exyz.x += wTheta * prx / lenA32;
                Exyz.y += wTheta * pry / lenA32;
                Exyz.z += wTheta * P.z / lenA32;
            }
            Exyz *= K;
            Exyz = (model * vec4(Exyz, 0.0)).xyz; // from disc to world vector
            return Exyz;
        }

        vec3 computeGKDisc(Disc disc, vec3 pos)
        {
            vec3 P = (inverse * vec4(pos, 1.0)).xyz; // from world to disc coordinates

            vec3 Exyz = vec3(0.0, 0.0, 0.0);

            float K = 1.0 / (4.0 * pi * E0);
            K *= disc.sigma;

            for(int i = 0; i < gk_nodes.length(); ++i) // r
            {
                for(int j = 0; j < gk_nodes.length(); ++j) // theta
                {
                    // calculating scaled nodes and weights (equations taken from https://en.wikipedia.org/wiki/Gauss%E2%80%93Kronrod_quadrature_formula)
                    float nR = (gk_nodes[i] + 1.0) / 2.0 * disc.radius;  // node for r
                    float nTheta = (gk_nodes[j] + 1.0) / 2.0 * 2.0 * pi; // node for theta

                    float wR = gk_weights[i] * disc.radius / 2.0;        // weight for r
                    float wTheta = gk_weights[j] * 2.0 * pi / 2.0;       // weight for theta

                    // finding distance squared of position
                    float prx = P.x - nR * cos(nTheta);
                    float prx2 = prx * prx;

                    float pry = P.y - nR * sin(nTheta);
                    float pry2 = pry * pry;

                    float pz2 = P.z * P.z;

                    float lenA2 = prx2 + pry2 + pz2;
                    float lenA32 = lenA2 * sqrt(lenA2); // lenA2^(3/2)

                    // calculating dExyz
                    Exyz.x += wR * wTheta * prx / lenA32 * nR;
                    Exyz.y += wR * wTheta * pry / lenA32 * nR;
                    Exyz.z += wR * wTheta * P.z / lenA32 * nR;
                }
            }
            Exyz *= K;
            Exyz = (model * vec4(Exyz, 0.0)).xyz; // from disc to world vector
            return Exyz;
        }

        vec3 computeGKWasher(Washer washer, vec3 pos)
        {
            vec3 P = (inverse * vec4(pos, 1.0)).xyz; // from world to disc coordinates

            vec3 Exyz = vec3(0.0, 0.0, 0.0);

            float K = 1.0 / (4.0 * pi * E0);
            K *= washer.sigma;

            for(int i = 0; i < gk_nodes.length(); ++i) // r
            {
                for(int j = 0; j < gk_nodes.length(); ++j) // theta
                {
                    // calculating scaled nodes and weights
                    float nR = (gk_nodes[i] + 1.0) / 2.0 * (washer.outer - washer.inner) + washer.inner;  // node for r
                    float nTheta = (gk_nodes[j] + 1.0) / 2.0 * 2.0 * pi;                                  // node for theta

                    float wR = gk_weights[i] * (washer.outer - washer.inner) / 2.0;        // weight for r
                    float wTheta = gk_weights[j] * 2.0 * pi / 2.0;                         // weight for theta

                    // finding distance squared of position
                    float prx = P.x - nR * cos(nTheta);
                    float prx2 = prx * prx;

                    float pry = P.y - nR * sin(nTheta);
                    float pry2 = pry * pry;

                    float pz2 = P.z * P.z;

                    float lenA2 = prx2 + pry2 + pz2;
                    float lenA32 = lenA2 * sqrt(lenA2); // lenA2^(3/2)

                    // calculating dExyz
                    Exyz.x += wR * wTheta * prx / lenA32 * nR;
                    Exyz.y += wR * wTheta * pry / lenA32 * nR;
                    Exyz.z += wR * wTheta * P.z / lenA32 * nR;
                }
            }
            Exyz *= K;
            Exyz = (model * vec4(Exyz, 0.0)).xyz; // from disc to world vector
            return Exyz;
        }

        void main()
        {
            // calculate electric field vector at vpos
            // point charge
            /*Point Q1;
            Q1.pos = pos;
            Q1.q = charge;*/

            // line segment charge
            /*LineSegment Q2;
            Q2.a = pos;
            Q2.b = pos2;
            Q2.q = charge * 4.0;*/

            // infinite plane charge
            /*Plane Q3;
            Q3.normal = normalize(pos2 - pos);
            Q3.pos = pos;
            Q3.sigma = charge * 0.2*/;

            // ring charge
            /*Ring Q4;
            vec3 dir = pos2 - pos;
            Q4.normal = normalize(dir);
            Q4.pos = pos;
            Q4.radius = length(dir);
            Q4.q = charge * 4.0;*/

            // disc charge
            /*Disc Q5;
            vec3 dir = pos2 - pos;
            Q5.normal = normalize(dir);
            Q5.pos = pos;
            Q5.radius = length(dir);
            Q5.sigma = charge / 2.0;*/

            // washer charge
            Washer Q6;
            vec3 dir = pos2 - pos;
            Q6.normal = normalize(dir);
            Q6.pos = pos;
            Q6.outer = length(dir);
            Q6.inner = Q6.outer / 2.0;
            Q6.sigma = charge;

            //vec3 z = computePoint(Q1, vpos);
            //vec3 z = computeLineSegment(Q2, vpos);
            //vec3 z = computePlane(Q3, vpos);
            //vec3 z = computeGKRing(Q4, vpos);
            //vec3 z = computeGKDisc(Q5, vpos);
            vec3 z = computeGKWasher(Q6, vpos);

            float mag = length(z);

            // generate color
            float col = clamp(mag, min_color, max_color);
            col = map(col, min_color, max_color, 0.0, 1.0);
            color = mix(vec3(0.0, 0.0117, 0.6784), vec3(0.941, 0.2039, 0.1215), col);

            // clamp z between min and max value
            float arrow_size = clamp(mag, 0.05, 1.0);
            z *= (arrow_size / mag);

            // calculate orientation matrix
            vec3 x = normalize(cross(vpos, vec3(0.0003, 1.0, 0.0001))) * arrow_size;
            vec3 y = normalize(cross(z, x)) * arrow_size;

            transform[0] = vec4(x, 0.0);//vec4(x, vpos.x);
            transform[1] = vec4(y, 0.0);//vec4(y, vpos.y);
            transform[2] = vec4(z, 0.0);//vec4(z, vpos.z);
            transform[3] = vec4(vpos, 1.0);
        }
    </script>
    <script id="ArrowComputeFragment-shader" type="fragment">
        #version 300 es
        precision highp float;
        void main() {}
    </script>
    <div>
        <button id="add-charge-button">Add Charge</button>
        <input type="range" min="1" max="100" value="10" id="test-slider">
    </div>
    <div id="controls">

    </div>
    <div class="container">
        <canvas id="c"></canvas>
    </div>
    <script src="gl-matrix.js"></script>
    <script src="shapes.js"></script>
    <script src="application.js"></script>
</body>
</html>